#!/usr/bin/env zsh
readonly SCRIPT=${0:a}
readonly SOURCE=${SCRIPT%/*}
readonly TARGET=$HOME
readonly PLUGINS=$SOURCE/vim/pack/plugins/start

typeset -a repos
readonly repos=(
  'https://github.com/fatih/vim-go'
  'https://github.com/godlygeek/tabular'
  'https://github.com/junegunn/fzf.vim'
  'https://github.com/nvim-tree/nvim-tree.lua'
  'https://github.com/nvim-tree/nvim-web-devicons'
  'https://github.com/hrsh7th/nvim-cmp'
  'https://github.com/hrsh7th/cmp-nvim-lsp'
  'https://github.com/hrsh7th/cmp-buffer'
  'https://github.com/hrsh7th/cmp-path'
  'https://github.com/hrsh7th/cmp-cmdline'
  'https://github.com/L3MON4D3/LuaSnip'
  'https://github.com/lukas-reineke/indent-blankline.nvim'
  'https://github.com/saadparwaiz1/cmp_luasnip'
  'https://github.com/norcalli/nvim-colorizer.lua'
  'https://github.com/neovim/nvim-lspconfig'
  'https://github.com/nvim-treesitter/nvim-treesitter'
  'https://github.com/onsails/lspkind-nvim'
  'https://github.com/scrooloose/nerdcommenter'
  'https://github.com/simrat39/symbols-outline.nvim'
  'https://github.com/tpope/vim-abolish'
  'https://github.com/tpope/vim-fugitive'
  'https://github.com/tpope/vim-surround'
  'https://github.com/vim-scripts/LaTeX-Box'
)

print_usage() {
  cat << EOF
Usage: $SCRIPT <COMMAND>

COMMANDS:
    install  Install vim config and plugins
    plugins  Install any plugins not yet installed
    update   Update/install all plugins
EOF
}

install_plugins() {
  local update_plugins=${1:-false}
  mkdir -p "$PLUGINS"
  for r in $repos; do
    local name=${r##*/}
    local plugin_path=$PLUGINS/$name
    if [[ -e "$plugin_path" ]]; then
      if $update_plugins; then
        echo "Updating $name..."
        ( cd "$plugin_path" && git pull --ff-only && git submodule update --init)
      fi
    else
      echo "Installing $name..."
      git clone --recursive "$r" "$plugin_path"
    fi
  done
  if command -v nvim > /dev/null; then
    nvim "+helptags ALL" +qall
  else
    vim "+helptags ALL" +qall
  fi
}

install() {
  local must_not_exist=("$TARGET/.vimrc" "$TARGET/.vim" "$TARGET/.config/nvim")
  for f in $must_not_exist; do
    if [[ -e "$f" || -L "$f" ]]; then
      echo "ERROR: '$f' already exists"
      exit 1
    fi
  done

  mkdir -p "$SOURCE/vim/swp"
  ln -s "$SOURCE/vim"   "$TARGET/.vim"
  ln -s "$SOURCE/vimrc" "$TARGET/.vimrc"
  ln -s "$SOURCE/vim"   "$TARGET/.config/nvim"
  ln -s "$SOURCE/vimrc" "$TARGET/.config/nvim/init.vim"

  install_plugins false
}

case $1 in
  install) install;;
  plugins) install_plugins false;;
  update) install_plugins true;;
  *) print_usage; exit 1;;
esac
